# Copyright (C) 2015 Forecast Foundation OU, full GPL notice in LICENSE

TOPICS = self.controller.lookup('topics')
extern topics: [getNumTopicsInBranch:[int256]:int256, getTopicPopularity:[int256,int256]:int256, getTopicsInBranch:[int256,int256,int256]:int256[], getTopicsInfo:[int256,int256,int256]:int256[], setController:[address]:int256, suicideFunds:[address]:_, updateTopicPopularity:[int256,int256,int256]:int256]

MARKETS = self.controller.lookup('markets')
extern markets: [addFees:[int256,int256]:int256, addToMarketsHash:[int256,int256]:int256, getBondsMan:[int256]:int256, getBranch:[int256]:int256, getCumulativeScale:[int256]:int256, getExtraInfo:[int256]:bytes, getExtraInfoLength:[int256]:int256, getFees:[int256]:int256, getGasSubsidy:[int256]:int256, getLastExpDate:[int256]:int256, getMarketEvent:[int256]:int256, getMarketNumOutcomes:[int256]:int256, getMarketResolved:[int256]:int256, getMarketShareContracts:[int256]:int256[], getMarketsHash:[int256]:int256, getOriginalTradingPeriod:[int256]:int256, getOutcomeShareContract:[int256,int256]:int256, getOutcomeShareWallet:[int256,int256]:int256, getParticipantSharesPurchased:[int256,int256,int256]:int256, getPushedForward:[int256]:int256, getSharesPurchased:[int256,int256]:uint256, getSharesValue:[int256]:int256, getTags:[int256]:int256[], getTopic:[int256]:int256, getTotalSharesPurchased:[int256]:int256, getTradingFee:[int256]:int256, getTradingPeriod:[int256]:int256, initializeMarket:[int256,int256,int256,int256,int256,int256,int256,int256,int256,int256,bytes,int256,int256,int256,int256[],int256[]]:int256, modifySharesValue:[int256,int256]:int256, refundClosing:[int256,int256]:int256, setController:[address]:int256, setMarketResolved:[int256]:int256, setPushedForward:[int256,int256,int256]:int256, setTradingFee:[int256,int256]:int256, setTradingPeriod:[int256,int256]:int256, suicideFunds:[address]:_]

extern controller: [addToWhitelist:[int256]:int256, assertIsWhitelisted:[int256]:int256, assertOnlySpecifiedCaller:[int256,int256]:_, changeMode:[int256]:int256, emergencyStop:[]:int256, getMode:[]:int256, getOwner:[]:int256, lookup:[int256]:int256, onlyInEmergency:[]:_, release:[]:int256, removeFromWhitelist:[int256,int256]:int256, setValue:[int256,int256]:int256, stopInEmergency:[]:_, suicide:[int256,int256,int256]:int256, switchModeSoOnlyEmergencyStopsAndEscapeHatchesCanBeUsed:[]:_, transferOwnership:[int256,int256,int256,int256]:int256, updateController:[int256,int256]:int256]

inset('../macros/safeMath.sem')
inset('../macros/assertZeroValue.sem')

### Storage of all data associated with orders

# indices: marketID, outcomeID, type, orderID
data orders[][][][](fxpAmount, fxpPrice, owner, fxpSharesEscrowed, fxpMoneyEscrowed, betterOrderID, worseOrderID, gasPrice)
# prices keeps track of the latest traded fxpPrice for a given outcome in a market [market is the key]
data marketOrderData[](volume, prices[])
# indices are marketID, outcomeID, and type
data bestOrder[][][]
data worstOrder[][][]

data controller

event CancelOrder(market: indexed, sender: indexed, fxpPrice, fxpAmount, orderID, outcome, type, cashRefund, sharesRefund, timestamp)
event CompleteSets(sender: indexed, market: indexed, type: indexed, fxpAmount, numOutcomes, fxpFee, timestamp)
event MakeOrder(market: indexed, sender: indexed, type, fxpPrice, fxpAmount, outcome, orderID, fxpMoneyEscrowed, fxpSharesEscrowed, timestamp, tradeGroupID)
event TakeAskOrder(market: address: indexed, sender: address: indexed, owner: address: indexed, type: uint256, fxpPrice, fxpAmount: uint256, timestamp: uint256, orderID: address, outcome: uint256, fxpAskerSharesFilled: uint256, fxpAskerMoneyFilled: uint256, fxpBidderMoneyFilled: uint256)
event TakeBidOrder(market: address: indexed, sender: address: indexed, owner: address: indexed, type: uint256, fxpPrice, fxpAmount: uint256, timestamp: uint256, orderID: address, outcome: uint256, fxpAskerSharesFilled: uint256, fxpAskerMoneyFilled: uint256, fxpBidderSharesFilled: uint256, fxpBidderMoneyFilled: uint256)

# Trade types
macro BID: 1
macro ASK: 2

def init():
    self.controller = 0x0

# @public
# @return fxp
def getAmount(orderID: address, type: uint256, market: address, outcome: uint256):
    return(self.orders[market][outcome][type][orderID].fxpAmount: uint256)

# @public
# @return fxp
def getPrice(orderID: address, type: uint256, market: address, outcome: uint256):
    return(self.orders[market][outcome][type][orderID].fxpPrice)

# @public
def getOrderOwner(orderID: address, type: uint256, market: address, outcome: uint256):
    return(self.orders[market][outcome][type][orderID].owner: address)

# @public
# @return fxp
def getVolume(market: address):
    return(self.marketOrderData[market].volume: uint256)

# @public
# @return fxp
def getLastOutcomePrice(market: address, outcome: uint256):
    return(self.marketOrderData[market].prices[outcome])

# Get orders for a particular market, type, and outcome (chunked)
# @public
def getOrderIDs(type: uint256, market: address, outcome: uint256, startingOrderID: address, numOrdersToLoad: uint256):
    if(type != BID and type != ASK):
        ~invalid()
    if(startingOrderID == 0):
        startingOrderID = self.bestOrder[market][outcome][type]
    orderIDs = array(numOrdersToLoad)
    orderIDs[0] = startingOrderID
    i = 0
    while(i < numOrdersToLoad and self.orders[market][outcome][type][orderIDs[i]].worseOrderID != 0):
        orderIDs[i + 1] = self.orders[market][outcome][type][orderIDs[i]].worseOrderID
        i += 1
    return(slice(orderIDs, items=0, items=i): arr)

# @public
def getOrder(orderID: address, type: uint256, market: address, outcome: uint256):
    order = array(13)
    if(self.orders[market][outcome][type][orderID].fxpAmount != 0):
        order[0] = orderID
        order[1] = type
        order[2] = market
        order[3] = self.orders[market][outcome][type][orderID].fxpAmount
        order[4] = self.orders[market][outcome][type][orderID].fxpPrice
        order[5] = self.orders[market][outcome][type][orderID].owner
        order[7] = outcome
        order[8] = self.orders[market][outcome][type][orderID].fxpMoneyEscrowed
        order[9] = self.orders[market][outcome][type][orderID].fxpSharesEscrowed
        order[10] = self.orders[market][outcome][type][orderID].betterOrderID
        order[11] = self.orders[market][outcome][type][orderID].worseOrderID
        order[12] = self.orders[market][outcome][type][orderID].gasPrice
    return(order: arr)

# @public
def getGasPrice(orderID: address, type: uint256, market: address, outcome: uint256):
    return(self.orders[market][outcome][type][orderID].gasPrice: uint256)

# @public
def getBestOrderID(type: uint256, market: address, outcome: uint256):
    return(self.bestOrder[market][outcome][type]: address)

# @public
def getWorstOrderID(type: uint256, market: address, outcome: uint256):
    return(self.worstOrder[market][outcome][type]: address)

# @public
def isBetterPrice(type: uint256, market: address, outcome: uint256, fxpPrice, orderID: address):
    if(type == BID):
        return((fxpPrice > self.orders[market][outcome][type][orderID].fxpPrice): uint256)
    else:
        return((fxpPrice < self.orders[market][outcome][type][orderID].fxpPrice): uint256)

# @public
def isWorsePrice(type: uint256, market: address, outcome: uint256, fxpPrice, orderID: address):
    if(type == BID):
        return((fxpPrice < self.orders[market][outcome][type][orderID].fxpPrice): uint256)
    else:
        return((fxpPrice > self.orders[market][outcome][type][orderID].fxpPrice): uint256)

# @public
def assertIsNotBetterPrice(type: uint256, market: address, outcome: uint256, fxpPrice, betterOrderID: address):
    if(self.isBetterPrice(type, market, outcome, fxpPrice, betterOrderID)):
        ~invalid()
    return(1: uint256)

# @public
def assertIsNotWorsePrice(type: uint256, market: address, outcome: uint256, fxpPrice, worseOrderID: address):
    if(self.isWorsePrice(type, market, outcome, fxpPrice, worseOrderID)):
        ~invalid()
    return(1: uint256)

# @public
def ascendOrderList(type: uint256, market: address, outcome: uint256, fxpPrice, lowestOrderID: address):
    worseOrderID = lowestOrderID
    if(type == BID):
        isWorstPrice = fxpPrice <= self.orders[market][outcome][type][worseOrderID].fxpPrice
    else:
        isWorstPrice = fxpPrice >= self.orders[market][outcome][type][worseOrderID].fxpPrice
    if(isWorstPrice):
        return([worseOrderID, self.orders[market][outcome][type][worseOrderID].worseOrderID]: arr)
    isBetterPrice = self.isBetterPrice(type, market, outcome, fxpPrice, worseOrderID)
    while(isBetterPrice and self.orders[market][outcome][type][worseOrderID].betterOrderID != 0 and fxpPrice != self.orders[market][outcome][type][self.orders[market][outcome][type][worseOrderID].betterOrderID].fxpPrice):
        isBetterPrice = self.isBetterPrice(type, market, outcome, fxpPrice, self.orders[market][outcome][type][worseOrderID].betterOrderID)
        if(isBetterPrice):
            worseOrderID = self.orders[market][outcome][type][worseOrderID].betterOrderID
    betterOrderID = self.orders[market][outcome][type][worseOrderID].betterOrderID
    return([betterOrderID, worseOrderID]: arr)

# @public
def descendOrderList(type: uint256, market: address, outcome: uint256, fxpPrice, highestOrderID: address):
    betterOrderID = highestOrderID
    if(type == BID):
        isBestPrice = fxpPrice > self.orders[market][outcome][type][betterOrderID].fxpPrice
    else:
        isBestPrice = fxpPrice < self.orders[market][outcome][type][betterOrderID].fxpPrice
    if(isBestPrice):
        return([0, betterOrderID]: arr)
    if(fxpPrice == self.orders[market][outcome][type][betterOrderID].fxpPrice):
        return([betterOrderID, self.orders[market][outcome][type][betterOrderID].worseOrderID]: arr)
    isWorsePrice = self.isWorsePrice(type, market, outcome, fxpPrice, betterOrderID)
    while(isWorsePrice and self.orders[market][outcome][type][betterOrderID].worseOrderID != 0):
        isWorsePrice = self.isWorsePrice(type, market, outcome, fxpPrice, self.orders[market][outcome][type][betterOrderID].worseOrderID)
        if(isWorsePrice or fxpPrice == self.orders[market][outcome][type][self.orders[market][outcome][type][betterOrderID].worseOrderID].fxpPrice):
            betterOrderID = self.orders[market][outcome][type][betterOrderID].worseOrderID
    worseOrderID = self.orders[market][outcome][type][betterOrderID].worseOrderID
    return([betterOrderID, worseOrderID]: arr)

# @public
def findBoundingOrders(type: uint256, market: address, outcome: uint256, fxpPrice, bestOrderID: address, worstOrderID: address, betterOrderID: address, worseOrderID: address):
    if(bestOrderID == worstOrderID):
        return([bestOrderID, bestOrderID]: arr)
    if(betterOrderID != 0):
        if(self.orders[market][outcome][type][betterOrderID].fxpAmount == 0):
            betterOrderID = 0
        else:
            self.assertIsNotBetterPrice(type, market, outcome, fxpPrice, betterOrderID)
    if(worseOrderID != 0):
        if(self.orders[market][outcome][type][worseOrderID].fxpAmount == 0):
            worseOrderID = 0
        else:
            self.assertIsNotWorsePrice(type, market, outcome, fxpPrice, worseOrderID)
    if(betterOrderID == 0 and worseOrderID == 0):
        return(self.descendOrderList(type, market, outcome, fxpPrice, bestOrderID, outitems=2): arr)
    elif(betterOrderID == 0):
        return(self.ascendOrderList(type, market, outcome, fxpPrice, worseOrderID, outitems=2): arr)
    elif(worseOrderID == 0):
        return(self.descendOrderList(type, market, outcome, fxpPrice, betterOrderID, outitems=2): arr)
    if(self.orders[market][outcome][type][betterOrderID].worseOrderID != worseOrderID):
        return(self.descendOrderList(type, market, outcome, fxpPrice, betterOrderID, outitems=2): arr)
    elif(self.orders[market][outcome][type][worseOrderID].betterOrderID != betterOrderID):
        return(self.ascendOrderList(type, market, outcome, fxpPrice, worseOrderID, outitems=2): arr)
    return([betterOrderID, worseOrderID]: arr)

# @internal
def insertOrderIntoList(orderID: address, type: uint256, market: address, outcome: uint256, fxpPrice, betterOrderID: address, worseOrderID: address):
    self.controller.assertIsWhitelisted(msg.sender)
    bestOrderID = self.bestOrder[market][outcome][type]
    worstOrderID = self.worstOrder[market][outcome][type]
    boundingOrders = self.findBoundingOrders(type, market, outcome, fxpPrice, bestOrderID, worstOrderID, betterOrderID, worseOrderID, outitems=2)
    betterOrderID = boundingOrders[0]
    worseOrderID = boundingOrders[1]
    if(type == BID):
        bestOrderID = self.updateBestBidOrder(orderID, market, fxpPrice, outcome, betterOrderID)
        worstOrderID = self.updateWorstBidOrder(orderID, market, fxpPrice, outcome, worseOrderID)
    else:
        bestOrderID = self.updateBestAskOrder(orderID, market, fxpPrice, outcome, betterOrderID)
        worstOrderID = self.updateWorstAskOrder(orderID, market, fxpPrice, outcome, worseOrderID)
    if(bestOrderID == orderID):
        betterOrderID = 0
    if(worstOrderID == orderID):
        worseOrderID = 0
    if(betterOrderID != 0):
        self.orders[market][outcome][type][betterOrderID].worseOrderID = orderID
        self.orders[market][outcome][type][orderID].betterOrderID = betterOrderID
    if(worseOrderID != 0):
        self.orders[market][outcome][type][worseOrderID].betterOrderID = orderID
        self.orders[market][outcome][type][orderID].worseOrderID = worseOrderID
    return(1: uint256)

# @internal
def saveOrder(orderID: address, type: uint256, market: address, fxpAmount: uint256, fxpPrice, sender: address, outcome: uint256, fxpMoneyEscrowed: uint256, fxpSharesEscrowed: uint256, betterOrderID: address, worseOrderID: address, tradeGroupID, gasPrice: uint256):
    self.controller.assertIsWhitelisted(msg.sender)
    if(type != BID and type != ASK):
        ~invalid()
    if(outcome > MARKETS.getMarketNumOutcomes(market)):
        ~invalid()
    self.insertOrderIntoList(orderID, type, market, outcome, fxpPrice, betterOrderID, worseOrderID)
    self.orders[market][outcome][type][orderID].fxpPrice = fxpPrice
    self.orders[market][outcome][type][orderID].fxpAmount = fxpAmount
    self.orders[market][outcome][type][orderID].owner = sender
    self.orders[market][outcome][type][orderID].fxpMoneyEscrowed = fxpMoneyEscrowed
    self.orders[market][outcome][type][orderID].fxpSharesEscrowed = fxpSharesEscrowed
    self.orders[market][outcome][type][orderID].gasPrice = gasPrice
    log(type=MakeOrder, market, sender, type, fxpPrice, fxpAmount, outcome, orderID, fxpMoneyEscrowed, fxpSharesEscrowed, block.timestamp, tradeGroupID)
    return(1: uint256)

# @internal
def removeOrderFromList(orderID: address, type: uint256, market: address, outcome: uint256):
    self.controller.assertIsWhitelisted(msg.sender)
    betterOrderID = self.orders[market][outcome][type][orderID].betterOrderID
    worseOrderID = self.orders[market][outcome][type][orderID].worseOrderID
    if(self.bestOrder[market][outcome][type] == orderID):
        self.bestOrder[market][outcome][type] = worseOrderID
    if(self.worstOrder[market][outcome][type] == orderID):
        self.worstOrder[market][outcome][type] = betterOrderID
    if(betterOrderID != 0):
        self.orders[market][outcome][type][betterOrderID].worseOrderID = worseOrderID
    if(worseOrderID != 0):
        self.orders[market][outcome][type][worseOrderID].betterOrderID = betterOrderID
    self.orders[market][outcome][type][orderID].betterOrderID = 0
    self.orders[market][outcome][type][orderID].worseOrderID = 0
    return(1: uint256)

# @internal
def removeOrder(orderID: address, type: uint256, market: address, outcome: uint256):
    self.controller.assertIsWhitelisted(msg.sender)
    self.removeOrderFromList(orderID, type, market, outcome)
    self.orders[market][outcome][type][orderID].fxpPrice = 0
    self.orders[market][outcome][type][orderID].fxpAmount = 0
    self.orders[market][outcome][type][orderID].owner = 0
    self.orders[market][outcome][type][orderID].fxpMoneyEscrowed = 0
    self.orders[market][outcome][type][orderID].fxpSharesEscrowed = 0
    self.orders[market][outcome][type][orderID].gasPrice = 0
    return(1: uint256)

# @internal
def fillOrder(orderID: address, type: uint256, market: address, outcome: uint256, fill, money, shares):
    self.controller.assertIsWhitelisted(msg.sender)
    if(fill > self.orders[market][outcome][type][orderID].fxpAmount):
        ~invalid()
    if(money > self.orders[market][outcome][type][orderID].fxpMoneyEscrowed):
        ~invalid()
    if(shares > self.orders[market][outcome][type][orderID].fxpSharesEscrowed):
        ~invalid()
    if(tx.gasprice > self.orders[market][outcome][type][orderID].gasPrice):
        ~invalid()
    self.orders[market][outcome][type][orderID].fxpAmount -= fill
    self.orders[market][outcome][type][orderID].fxpMoneyEscrowed -= money
    self.orders[market][outcome][type][orderID].fxpSharesEscrowed -= shares
    return(1: uint256)

# Log transaction [ASK b/c it's filling a bid so from trader's perspective they're asking]
# @ internal
def takeBidOrderLog(market: address, sender: address, owner: address, fxpPrice, fxpSumOfFills: uint256, orderID: address, outcome: uint256, fxpAskerSharesFilled: uint256, fxpAskerMoneyFilled: uint256, fxpBidderSharesFilled: uint256, fxpBidderMoneyFilled: uint256):
    self.controller.assertIsWhitelisted(msg.sender)
    log(type=TakeBidOrder, market, sender, owner, ASK, fxpPrice, fxpSumOfFills, block.timestamp, orderID, outcome, fxpAskerSharesFilled, fxpAskerMoneyFilled, fxpBidderSharesFilled, fxpBidderMoneyFilled)
    return(1: uint256)

# Log transaction [BID b/c it's filling an ask so from trader's perspective they're bidding]
# @ internal
def takeAskOrderLog(market: address, sender: address, owner: address, fxpPrice, fxpSumOfFills: uint256, orderID: address, outcome: uint256, fxpAskerSharesFilled: uint256, fxpAskerMoneyFilled: uint256, fxpBidderMoneyFilled: uint256):
    self.controller.assertIsWhitelisted(msg.sender)
    log(type=TakeAskOrder, market, sender, owner, BID, fxpPrice, fxpSumOfFills, block.timestamp, orderID, outcome, fxpAskerSharesFilled, fxpAskerMoneyFilled, fxpBidderMoneyFilled)
    return(1: uint256)

# @ internal
def completeSetsLog(sender, market, type, fxpAmount, numOutcomes, fee):
    self.controller.assertIsWhitelisted(msg.sender)
    log(type=CompleteSets, sender, market, type, fxpAmount, numOutcomes, fee, block.timestamp)
    return(1: uint256)

# @ internal
def cancelOrderLog(market, sender, fxpPrice, fxpAmount, orderID, outcome, type, fxpMoneyEscrowed, fxpSharesEscrowed):
    self.controller.assertIsWhitelisted(msg.sender)
    log(type=CancelOrder, market, sender, fxpPrice, fxpAmount, orderID, outcome, type, fxpMoneyEscrowed, fxpSharesEscrowed, block.timestamp)
    return(1: uint256)

# @internal
def modifyMarketVolume(market: address, fxpAmount):
    self.controller.assertIsWhitelisted(msg.sender)
    self.marketOrderData[market].volume += fxpAmount
    TOPICS.updateTopicPopularity(MARKETS.getBranch(market), MARKETS.getTopic(market), fxpAmount)
    return(1: uint256)

# @internal
def setPrice(market: address, outcome: uint256, fxpPrice):
    self.controller.assertIsWhitelisted(msg.sender)
    self.marketOrderData[market].prices[outcome] = fxpPrice
    return(1: uint256)

# If best bid is not set or price higher than best bid price, this order is the new best bid.
# @private
def updateBestBidOrder(orderID: address, market: address, fxpPrice, outcome: uint256, betterOrderID: uint256):
    if(msg.sender != self):
        ~invalid()
    bestBidOrderID = self.bestOrder[market][outcome][BID]
    if(bestBidOrderID == 0 or fxpPrice > self.orders[market][outcome][BID][bestBidOrderID].fxpPrice):
        self.bestOrder[market][outcome][BID] = orderID
    return(self.bestOrder[market][outcome][BID]: address)

# If worst bid is not set or price lower than worst bid price, this order is the new worst bid.
# @private
def updateWorstBidOrder(orderID: address, market: address, fxpPrice, outcome: uint256, worseOrderID: uint256):
    if(msg.sender != self):
        ~invalid()
    worstBidOrderID = self.worstOrder[market][outcome][BID]
    if(worstBidOrderID == 0 or fxpPrice < self.orders[market][outcome][BID][worstBidOrderID].fxpPrice):
        self.worstOrder[market][outcome][BID] = orderID
    return(self.worstOrder[market][outcome][BID]: address)

# If best ask is not set or price lower than best ask price, this order is the new best ask.
# @private
def updateBestAskOrder(orderID: address, market: address, fxpPrice, outcome: uint256, betterOrderID: uint256):
    if(msg.sender != self):
        ~invalid()
    bestAskOrderID = self.bestOrder[market][outcome][ASK]
    if(bestAskOrderID == 0 or fxpPrice < self.orders[market][outcome][ASK][bestAskOrderID].fxpPrice):
        self.bestOrder[market][outcome][ASK] = orderID
    return(self.bestOrder[market][outcome][ASK]: address)

# If worst ask is not set or price higher than worst ask price, this order is the new worst ask.
# @private
def updateWorstAskOrder(orderID: address, market: address, fxpPrice, outcome: uint256, worseOrderID: uint256):
    if(msg.sender != self):
        ~invalid()
    worstAskOrderID = self.worstOrder[market][outcome][ASK]
    if(worstAskOrderID == 0 or fxpPrice > self.orders[market][outcome][ASK][worstAskOrderID].fxpPrice):
        self.worstOrder[market][outcome][ASK] = orderID
    return(self.worstOrder[market][outcome][ASK]: address)

# @controller
def setController(newController: address):
    if(msg.sender != self.controller):
        ~invalid()
    self.controller = newController
    return(1)

# @controller
def suicideFunds(to: address):
    if(msg.sender != self.controller):
        ~invalid()
    suicide(to)
