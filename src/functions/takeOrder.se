# Copyright (C) 2015 Forecast Foundation OU, full GPL notice in LICENSE

extern controller: [addToWhitelist:[int256]:int256, assertIsWhitelisted:[int256]:int256, assertOnlySpecifiedCaller:[int256,int256]:_, changeMode:[int256]:int256, emergencyStop:[]:int256, getMode:[]:int256, getOwner:[]:int256, lookup:[int256]:int256, onlyInEmergency:[]:_, release:[]:int256, removeFromWhitelist:[int256,int256]:int256, setValue:[int256,int256]:int256, stopInEmergency:[]:_, suicide:[int256,int256,int256]:int256, switchModeSoOnlyEmergencyStopsAndEscapeHatchesCanBeUsed:[]:_, transferOwnership:[int256,int256,int256,int256]:int256, updateController:[int256,int256]:int256]

MUTEX = self.controller.lookup('mutex')
extern mutex: [acquire:[]:int256, release:[]:int256, setController:[address]:int256, suicideFunds:[address]:_]

FILLASKORDER = self.controller.lookup('takeAskOrder')
extern takeAskOrder: [setController:[address]:int256, suicideFunds:[address]:_, takeAskOrder:[address,address,uint256]:uint256]

TAKEBIDORDER = self.controller.lookup('takeBidOrder')
extern takeBidOrder: [setController:[address]:int256, suicideFunds:[address]:_, takeBidOrder:[address,address,uint256]:uint256]

extern subcurrency: [allowance:[address,address]:uint256, approve:[address,uint256]:uint256, balanceOf:[address]:uint256, decimals:[]:uint256, name:[]:uint256, symbol:[]:uint256, totalSupply:[]:uint256, transfer:[address,uint256]:uint256, transferFrom:[address,address,uint256]:uint256]

inset('../macros/assertZeroValue.sem')

data controller

# Trade types
macro BID: 1
macro ASK: 2

### Allows users to take orders off the book (i.e. fill orders)

# Trade scenarios:
# Note: all [] scenarios are typical, if a user somehow gets an extra complete set it'll also be sold when doing a order, typically complete sets only sold when closing out a position or closing out a short
# - Buying and have all but one and bidding, seller doesn't have outcome and is taking [complete set fee charged on buyer b/c selling complete sets].
# - Buying and have all but one and bidding, seller has outcome and is taking [complete set fee charged on buyer].
# - Buying and don't own anything and bidding, seller doesn't have outcome and is taking [no complete set fee charged b/c none sold].
# - Buying and don't own anything and bidding, seller has outcome and is taking [no complete set fee charged b/c none sold].
# - Buying and have all but one and taking, seller doesn't have outcome and is asking [complete set fee charged on buyer].
# - Buying and have all but one and taking, seller has outcome and is asking [complete set fee charged on buyer].
# - Buying and don't own anything and taking, seller doesn't have outcome and is asking [no complete set fee charged].
# - Buying and don't own anything and taking, seller has outcome and is asking [no complete set fee charged].
#
# publicTakeOrder [allows a user to pick up/fill orders off the book]
#
# fxpAmountTakerWants is amount of shares the taker wants to fill
# picks orders off the book

def init():
    self.controller = 0x0

# "Snipe" an order on the orderbook using its ID.
# @public
# @return fxp
def publicTakeOrder(orderID: address, type: uint256, market: address, outcome: uint256, fxpAmountTakerWants: uint256):
    assertZeroValue()
    MUTEX.acquire()
    self.controller.stopInEmergency()
    fxpAmountRemaining = self.takeOrder(msg.sender, orderID, type, market, outcome, fxpAmountTakerWants)
    MUTEX.release()
    return(fxpAmountRemaining: uint256)

# @internal
# @return fxp
def takeOrder(sender: address, orderID: address, type: uint256, market: address, outcome: uint256, fxpAmountTakerWants: uint256):
    assertZeroValue()
    self.controller.assertIsWhitelisted(msg.sender)
    if(fxpAmountTakerWants <= 0):
        ~invalid()
    # Fill orders
    if(type == BID):
        fxpAmountRemaining = TAKEBIDORDER.takeBidOrder(sender, orderID, market, outcome, fxpAmountTakerWants, call=delegate)
    elif(type == ASK):
        fxpAmountRemaining = FILLASKORDER.takeAskOrder(sender, orderID, market, outcome, fxpAmountTakerWants, call=delegate)
    else:
        ~invalid()
    return(fxpAmountRemaining: uint256)

def setController(newController: address):
    if(msg.sender != self.controller):
        ~invalid()
    self.controller = newController
    return(1)

def suicideFunds(to: address):
    if(msg.sender != self.controller):
        ~invalid()
    suicide(to)
