import branches as BRANCHES
import info as INFO
import cash as CASH
import markets as MARKETS
import events as EVENTS
import expiringEvents as EXPEVENTS
inset('refund.se')

macro YES: TWO
macro NO: ONE
macro BAD: 3 * ONEHALF
macro CATCH_TOLERANCE: ONE / 10

# Bins values to 1, 1.5, 2
macro catch($x):
    if($x < (BAD - CATCH_TOLERANCE)):
        NO
    elif($x > (BAD + CATCH_TOLERANCE)):
        YES
    else:
        BAD

macro ethic_catch($x):
    if($x < ONEHALF):
        0
    else:
        ONE

macro scalar($event):
    ((EVENTS.getMaxValue($event)!=TWO || EVENTS.getMinValue($event)!=ONE) && EVENTS.getNumOutcomes($event)==2)

## helper function to close an event for resolution
macro closeOut($event, $market, $branch, $votingPeriodEvent, $fxpOutcome, $periodLength, $ethical, $scalarorcat, $mode, $ethicOutcome):
    # In the $event 'Market is not ready to resolve' is found to be the consensus, the early resolution bond is paid to the reporters for their labor.
    # and $market remains with old expiration date
    # UI needs to call penalizeWrong for ones that are pushed back in both pushed back period and regular period with actual reporting
    $indeterminate = ONEHALF*3
    if(($fxpOutcome == $indeterminate or $mode==ONEHALF) && EVENTS.getEventPushedUp($event)):
        if(MARKETS.getPushedForward($market)):
            CASH.addCash($branch, MARKETS.getTradingFee($market)*MARKETS.getTotalSharesPurchased($market)/(2*ONE))
            CASH.subtractCash($market, MARKETS.getTradingFee($market)*MARKETS.getTotalSharesPurchased($market)/(2*ONE))
        MARKETS.setTradingPeriod($market, MARKETS.getOriginalTradingPeriod($market))
        MARKETS.setPushedForward($market, 0, msg.sender)
        EXPEVENTS.setEventWeight($branch, $votingPeriodEvent, $event, 0)
        #set outcome to 0
        EVENTS.setOutcome($event, 0)
        EVENTS.setMode($event, 0)
        EVENTS.setEthics($event, 0)
        #set uncaughtoutcome to 0
        EVENTS.setUncaughtOutcome($event, 0)
        EVENTS.setEventPushedUp($event, 0)
        # need to store which period it was "rejected" in
        EVENTS.setRejected($event, EVENTS.getExpiration($event)/$periodLength)
        #set $event expiration to be the old date
        EVENTS.setExpiration($event, EVENTS.getOriginalExpiration($event))
        -6
    elif(($fxpOutcome==$indeterminate or $mode==ONEHALF) || !$ethical):
        # give $event bond money to reporters
        CASH.subtractCash($event, EVENTS.getBond($event))
        CASH.addCash($branch, EVENTS.getBond($event))
        # not $ethical is same as .5 outcome
        if($scalarorcat):
            # the mode is used to compare for reporting accuracy. so still the same as the reported uncaught outcome/mode, but resolving the market as .5 right in the middle of the range so payouts are done right for an unethical [or indeterminate] market
            EVENTS.setOutcome($event, $ethicOutcome)
        else:
            EVENTS.setOutcome($event, $indeterminate)
        1
    else:
        # return bond as a normal event resolution
        CASH.subtractCash($event, EVENTS.getBond($event))
        CASH.addCash(INFO.getCreator($event), EVENTS.getBond($event))
        if(MARKETS.getPushedForward($market)):
            CASH.addCash(MARKETS.getBondsMan($market), MARKETS.getTradingFee($market)*MARKETS.getTotalSharesPurchased($market)/(2*ONE))
            CASH.subtractCash($market, MARKETS.getTradingFee($market)*MARKETS.getTotalSharesPurchased($market)/(2*ONE))
        if(EVENTS.getEventPushedUp($event)):
            # delete old $event from future events exp
            $period = EVENTS.getOriginalExpiration($event)/$periodLength
            EXPEVENTS.removeEvent($branch, $period)
            EXPEVENTS.deleteEvent($branch, $period, $event)
            EVENTS.setOriginalExpiration($event, EVENTS.getExpiration($event))
        1

# Resolves a binary market
def resolveBinary(event, market, branch, votingPeriodEvent):
    refund()
    fxpOutcome = catch(EVENTS.getUncaughtOutcome(event))
    periodLength = BRANCHES.getPeriodLength(branch)
    ethical = ethic_catch(EVENTS.getEthical(event))
    # outcome is set to the binned version of the raw reported outcome
    EVENTS.setOutcome(event, fxpOutcome)
    # ethics is set to the binned version of the raw ethicality outcome
    EVENTS.setEthics(event, ethical)
    val = closeOut(event, market, branch, votingPeriodEvent, fxpOutcome, periodLength, ethical, 0, 0, 0)
    return(val)

# Resolves a categorical market
def resolveCategoricalOrScalar(scaled_min, scaled_max, event, market, branch, votingPeriodEvent):
    refund()
    mode = EVENTS.getUncaughtOutcome(event)
    EVENTS.setMode(event, mode)
    fxpOutcome = mode * (scaled_max - scaled_min)/ONE + scaled_min
    # outcome is set to the mode of the reported outcomes
    EVENTS.setOutcome(event, fxpOutcome)
    periodLength = BRANCHES.getPeriodLength(branch)
    # ethics is set to the binned version of the raw ethicality outcome
    ethical = ethic_catch(EVENTS.getEthical(event))
    ethicOutcome = 2**63 * (scaled_max - scaled_min)/ONE + scaled_min
    EVENTS.setEthics(event, ethical)
    val = closeOut(event, market, branch, votingPeriodEvent, fxpOutcome, periodLength, ethical, 1, mode, ethicOutcome)
    return(val)

# Determines the "winning outcomes" for a given market
# Easiest to give examples:
    # 1D Markets:
        # Binary or Categorical will have 1 winning outcome [1 or 2 for binary, 1 thru numOutcomes for categorical is the poss. value for the winning outcome]
def determineWinningOutcomes(event):
    refund()
    winningOutcomes = array(8)
    fxpOutcome = EVENTS.getOutcome(event)
    outcome = fxpOutcome/ONE
    #scalar or .5
    if(scalar(event) || fxpOutcome==3*ONEHALF):
        winningOutcomes[0] = 1
        winningOutcomes[1] = 2
    # anything besides scalar or .5
    else:
        winningOutcomes[0] += outcome
    return(winningOutcomes: arr)